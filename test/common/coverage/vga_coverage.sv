`uvm_analysis_imp_decl(_in)
`uvm_analysis_imp_decl(_colour)
`uvm_analysis_imp_decl(_timing)

class vga_coverage extends uvm_component;
  `uvm_component_utils(vga_coverage)

  uvm_analysis_imp_in #(screen_buttons_transaction, vga_coverage) input_export;
  uvm_analysis_imp_colour #(vga_transaction, vga_coverage) colour_export;
  uvm_analysis_imp_timing #(timing_transaction, vga_coverage) timing_export;

  real in_coverage;
  real colour_coverage;
  real sync_coverage;

  covergroup in_cg with function sample(bit rst, bit bt1, bit bt2);
    option.per_instance = 1;

    cp_rst: coverpoint rst {
      bins low  = { 0 };
      bins high  = { 1 };
    }

    cp_bt1: coverpoint bt1 {
      bins zero_to_zero  = ( 0 => 0 );
      bins zero_to_one  = ( 0 => 1 );
      bins one_to_zero  = ( 1 => 0 );
      bins one_to_one  = ( 1 => 1 );
    }

    cp_bt2: coverpoint bt2 {
      bins zero_to_zero  = ( 0 => 0 );
      bins zero_to_one  = ( 0 => 1 );
      bins one_to_zero  = ( 1 => 0 );
      bins one_to_one  = ( 1 => 1 );
    }

    cross_bt1_rst: cross cp_bt1, cp_rst;
    cross_bt2_rst: cross cp_bt2, cp_rst;
  endgroup

  covergroup colour_cg with function sample(bit [1:0] r, bit [1:0] g, bit [1:0] b);
    option.per_instance = 1;

    cp_r: coverpoint r {
      bins low  = { 0 };
      bins dithered  = { [1:2] };
      bins high  = { 3 };
    }

    cp_g: coverpoint g {
      bins low  = { 0 };
      bins dithered  = { [1:2] };
      bins high  = { 3 };
    }

    cp_b: coverpoint b {
      bins low  = { 0 };
      bins dithered  = { [1:2] };
      bins high  = { 3 };
    }

  endgroup

  covergroup timing_cg with function sample(bit hsync, bit vsync);
    option.per_instance = 1;

    cp_hsync: coverpoint hsync {
      bins zero  = { 0 };
      bins one  =  { 1 };
    }

    cp_vsync: coverpoint vsync {
      bins zero  = { 0 };
      bins one  =  { 1 };
    }

    cross_sync: cross cp_hsync, cp_vsync;
  endgroup

  function new(string name = "vga_coverage", uvm_component parent);
    super.new(name, parent);
    in_cg = new();
    colour_cg = new();
    timing_cg = new();
    input_export = new("input_export", this);
    colour_export = new("colour_export", this);
    timing_export = new("timing_export", this);
  endfunction

  virtual function void write_in(screen_buttons_transaction trans);
    in_cg.sample(trans.rst_trans.rst, trans.bt1_trans.out, trans.bt2_trans.out);
  endfunction

  virtual function void write_colour(vga_transaction trans);
    colour_cg.sample(trans.r, trans.g, trans.b);
  endfunction

  virtual function void write_timing(timing_transaction trans);
    timing_cg.sample(trans.hsync, trans.vsync);
  endfunction

  virtual function void extract_phase(uvm_phase phase);
    super.extract_phase(phase);
    in_coverage = in_cg.get_inst_coverage();
    colour_coverage = colour_cg.get_inst_coverage();
    sync_coverage = timing_cg.get_inst_coverage();
  endfunction

  virtual function void report_phase(uvm_phase phase);
    super.report_phase(phase);
    `uvm_info("COV_REPORT", $sformatf("in coverage: %0f", in_coverage), UVM_LOW)
    `uvm_info("COV_REPORT", $sformatf("colour coverage: %0f", colour_coverage), UVM_LOW)
    `uvm_info("COV_REPORT", $sformatf("sync coverage: %0f", sync_coverage), UVM_LOW)
  endfunction
endclass
